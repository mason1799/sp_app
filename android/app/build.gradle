plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId 'com.kone.konesp'
        minSdkVersion 24
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    flavorDimensions 'env'

    productFlavors {
        prod {
            dimension 'env'
            applicationId = 'com.kone.konesp'
            manifestPlaceholders = [
                    JPUSH_PKGNAME: applicationId,
                    JPUSH_APPKEY : 'f95ffbaa0c9ee70c5502f3dc',
                    JPUSH_CHANNEL: 'konesp',
                    APP_NAME     : '仟帆',
                    APP_ICON     : '@mipmap/ic_launcher'
            ]
        }
        qa {
            dimension 'env'
            applicationId = 'com.kone.qakonesp'
            manifestPlaceholders = [
                    JPUSH_PKGNAME: applicationId,
                    JPUSH_APPKEY : 'dd9010c0a9829778bc8fbe00',
                    JPUSH_CHANNEL: 'qakonesp',
                    APP_NAME     : '仟帆qa',
                    APP_ICON     : '@mipmap/ic_launcher_test'
            ]
        }
        dev {
            dimension 'env'
            applicationId = 'com.kone.devkonesp'
            manifestPlaceholders = [
                    JPUSH_PKGNAME: applicationId,
                    JPUSH_APPKEY : '4715d0d4fe1224ad9696e84a',
                    JPUSH_CHANNEL: 'devkonesp',
                    APP_NAME     : '仟帆dev',
                    APP_ICON     : '@mipmap/ic_launcher_test'
            ]
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources false
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources false
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation('com.amap.api:location:5.2.0')
}
